//
// build.gradle
//
// Copyright (c) 2017, 2018, 2019 Couchbase, Inc All rights reserved.
//
// Licensed under the Couchbase License Agreement (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
// https://info.couchbase.com/rs/302-GJY-034/images/2017-10-30_License_Agreement.pdf
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.
//

// ----------------------------------------------------------------
// Constants
// ----------------------------------------------------------------
ext {
    CBL_GROUP = 'com.couchbase.lite'

    BUILD_VERSION = file("$ROOT_DIR/version.txt").text.trim()
    BUILD_NUMBER = (!project.hasProperty("buildNumber")) ? "SNAPSHOT" : buildNumber

    REPORTS_DIR = "${buildDir}/reports"
    CBL_CORE_DIR = "${ROOT_DIR}/couchbase-lite-core"
    CBL_JAVA_DIR = "${ROOT_DIR}/couchbase-lite-java/lib"
    CBL_ANDROID_DIR = "${PROJECT_DIR}"
    REPORTS_DIR = "${buildDir}/reports"
}

// comma separated list of annotations for tests that should not be run.
def TEST_FILTER = (!project.hasProperty("testFilter")) ? null : testFilter

// ----------------------------------------------------------------
// Build
// ----------------------------------------------------------------
apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

group = CBL_GROUP
version = BUILD_VERSION

android {
    compileSdkVersion COMPILE_SDK_VERSION
    buildToolsVersion BUILD_TOOLS_VERSION

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 27
        versionCode 1
        versionName BUILD_VERSION

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        if (TEST_FILTER != null) {
            testInstrumentationRunnerArguments notAnnotation: TEST_FILTER
        }
    }

    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }

    sourceSets {
        androidTest {
            java.srcDirs = [
                "${CBL_ANDROID_DIR}/lib/src/androidTest/java", // android specific codes
                "${CBL_JAVA_DIR}/src/shared/test/java"         // shared java tests
            ]
            assets.srcDirs = [
                "${CBL_CORE_DIR}/C/tests/data",                // lite-core test assets
                "${CBL_JAVA_DIR}/src/shared/test/assets"       // shared assets
            ]
        }
    }
}

repositories {
    mavenLocal()
    maven { url "http://mobile.maven.couchbase.com/maven2/cimaven/" }
    google()
    jcenter()
}

dependencies {
    implementation "com.couchbase.lite:couchbase-lite-android:${BUILD_VERSION}-${BUILD_NUMBER}"

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$KOTLIN_VERSION"

    testImplementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$KOTLIN_VERSION"
    testImplementation 'junit:junit:4.12'

    androidTestImplementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$KOTLIN_VERSION"
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}


// ----------------------------------------------------------------
// Tasks
// ----------------------------------------------------------------

// This target requires specifying properties "buildNumber" and "automatedTests".
// See settings.gradle for explanation.
task ciTest(dependsOn: ['connectedDebugAndroidTest'])
