apply plugin: 'com.android.library'
apply plugin: 'maven'

// This property value is for couchbase-lite-java-forestdb
ext.spec = "android"

version = System.getenv("MAVEN_UPLOAD_VERSION")

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.1.2'
    }
}

repositories {
    mavenLocal()
    maven { url 'http://files.couchbase.com/maven2/' }
    mavenCentral()
}

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_6
        targetCompatibility JavaVersion.VERSION_1_6
    }

    buildTypes {
        release {
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    lintOptions {
        abortOnError false
    }

    // workaround for "duplicate files during packaging of APK" issue
    // see https://groups.google.com/d/msg/adt-dev/bl5Rc4Szpzg/wC8cylTWuIEJ
    packagingOptions {
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }
}

def buildAndroidWithArtifacts = System.getProperty("buildAndroidWithArtifacts")

dependencies {
    compile buildAndroidWithArtifacts == null ?
            project(':libraries:couchbase-lite-java-core') :
            'com.couchbase.lite:couchbase-lite-java-core:' + version

    compile buildAndroidWithArtifacts == null ?
            project(':libraries:couchbase-lite-java-native:sqlite-custom') :
            'com.couchbase.lite:couchbase-lite-android-sqlite-custom:' + version

    // Libraries required for tests only:
    androidTestCompile buildAndroidWithArtifacts == null ?
            project(':libraries:couchbase-lite-java-native:sqlcipher') :
            'com.couchbase.lite:couchbase-lite-android-sqlcipher:' + version

    androidTestCompile buildAndroidWithArtifacts == null ?
            project(':libraries:couchbase-lite-java-forestdb') :
            'com.couchbase.lite:couchbase-lite-android-forestdb:' + version

    androidTestCompile 'commons-io:commons-io:2.0.1'
    androidTestCompile 'com.squareup.okhttp3:mockwebserver:3.11.0'

    // For JavaScript query test
    androidTestCompile ('com.couchbase.lite:couchbase-lite-java-javascript:1.4.+'){
        exclude group: 'com.couchbase.lite', module: 'couchbase-lite-java-core'
    }
}

task generateJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    List<File> pathList = new ArrayList<File>();
    pathList.add(file('./libraries/couchbase-lite-java-core/extra/doclet/doclet.jar'))
    options.docletpath = pathList
    options.doclet = "ExcludeDoclet"
    options.showFromPublic()
    exclude "org/apache/http/**", "com/couchbase/touchdb/**"
}

task createMavenDirectory(type: Exec) {
    //these envs are required only to upload maven artifacts
    print project.getGradle()
    if (project.getGradle().toString().contains("createMavenDirectory")) {
        if (System.getenv("MAVEN_UPLOAD_REPO_URL") == null) {
            throw new GradleException("Please set MAVEN_UPLOAD_REPO_URL !!!")
        }
        if (System.getenv("MAVEN_UPLOAD_USERNAME") == null) {
            throw new GradleException("Please set MAVEN_UPLOAD_USERNAME !!!")
        }
        if (System.getenv("MAVEN_UPLOAD_PASSWORD") == null) {
            throw new GradleException("Please set MAVEN_UPLOAD_PASSWORD !!!")
        }
        if (System.getenv("MAVEN_UPLOAD_VERSION") == null) {
            throw new GradleException("Please set MAVEN_UPLOAD_VERSION !!!")
        }
    }

    ext {
        uploadUser = System.getenv("MAVEN_UPLOAD_USERNAME") + ":" + System.getenv("MAVEN_UPLOAD_PASSWORD")
        mkcolPath = System.getenv("MAVEN_UPLOAD_REPO_URL") + "com/couchbase/lite/couchbase-lite-android/" + System.getenv("MAVEN_UPLOAD_VERSION") + "/"
    }
    commandLine "curl", "--user", uploadUser, "-X", "MKCOL", mkcolPath
}

// this hack is only needed for apache mod_dav based Maven repo's like file.couchbase.com.  otherwise, skip it
createMavenDirectory.onlyIf { System.getenv("MAVEN_UPLOAD_REPO_URL").contains("files") }

// first create the directory, then do the upload
task uploadArchivesWrapper(dependsOn: createMavenDirectory) << {
    uploadArchives.execute()
}

def mavenPath() {
    System.getenv("MAVEN_BUILD_LOCAL") == "true" ?
            'file://' + new File(System.getProperty('user.home'), '.m2/repository').absolutePath :
            System.getenv("MAVEN_UPLOAD_REPO_URL")
}

// this will upload, but will not first create a directory (which is needed on some servers)
uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: mavenPath()) {
                authentication(userName: System.getenv("MAVEN_UPLOAD_USERNAME"), password: System.getenv("MAVEN_UPLOAD_PASSWORD"))
            }
            pom.version = System.getenv("MAVEN_UPLOAD_VERSION") != null ? System.getenv("MAVEN_UPLOAD_VERSION") : ''
            pom.groupId = 'com.couchbase.lite'
            pom.artifactId = 'couchbase-lite-android'
            pom.project {
                name 'com.couchbase.lite:couchbase-lite-android'
                description 'Couchbase Lite is an embedded lightweight, document-oriented (NoSQL), syncable database engine.'
                url 'http://developer.couchbase.com/mobile/'
                licenses {
                    license {
                        name 'Couchbase, Inc. Community Edition License Agreement'
                        url 'https://www.couchbase.com/binaries/content/assets/website/legal/ce-license-agreement.pdf'
                        distribution 'repo'
                    }
                }
                scm {
                    url 'http://developer.couchbase.com/mobile/'
                    connection 'scm:git@github.com:couchbase/couchbase-lite-android.git'
                    developerConnection 'scm:git@github.com:couchbase/couchbase-lite-android.git'
                }
                developers {
                    developer {
                        name 'Couchbase Mobile'
                        email 'mobile@couchbase.com'
                        organization 'Couchbase'
                        organizationUrl 'http://www.couchbase.com'
                    }
               }
            }
        }
    }
}


task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives sourcesJar
}
