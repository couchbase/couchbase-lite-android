buildscript {
    repositories {
        maven { url 'http://repo1.maven.org/maven2' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.4.1'
    }
}
apply plugin: 'android-library'
apply plugin: 'maven'

repositories {
    mavenCentral()
}

dependencies {
    // jackson libraries declared here only as a workaround for issue: https://groups.google.com/forum/?fromgroups#!topic/adt-dev/Efpf87EoDQ0
    compile files('libs/android-support-v4.jar', 'libs/jackson-core-asl-1.9.2.jar', 'libs/jackson-mapper-asl-1.9.2.jar')

    instrumentTestCompile 'commons-io:commons-io:2.0.1'

    // jackson libraries are being declared here only for purposes of generating a correct maven .pom
    // that lists these as dependencies.  otherwise, the static jars listed above are used for actually meeting dependencies
    // because of this issue: https://groups.google.com/forum/?fromgroups#!topic/adt-dev/Efpf87EoDQ0
    compile 'org.codehaus.jackson:jackson-core-asl:1.9.2'
    compile 'org.codehaus.jackson:jackson-mapper-asl:1.9.2'

}

android {
    compileSdkVersion 17
    buildToolsVersion "17.0.0"

    defaultConfig {
        minSdkVersion 7
        targetSdkVersion 16
    }

}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: uri("../testrepo"))
            pom.version = '1.0'
            pom.groupId = 'com.couchbase.cblite'
            pom.artifactId = 'CBLite'
        }
    }
}

// /begin .so hack - this is a hack until gradle supports dependencies on .so objects
// the hack is described here:
//      http://stackoverflow.com/questions/16683775/include-so-library-in-apk-in-android-studio
// I had to modify it slightly to change buildDir -> getProjectDir()

task copyNativeLibs(type: Copy) {
    from(new File(project(':CBLite').getProjectDir(), 'native-libs')) { include '**/*.so' }
    into new File(buildDir, 'native-libs')
}

tasks.withType(Compile) { compileTask -> compileTask.dependsOn copyNativeLibs }

clean.dependsOn 'cleanCopyNativeLibs'

tasks.withType(com.android.build.gradle.tasks.PackageApplication) { pkgTask ->
    pkgTask.jniDir new File(buildDir, 'native-libs')
}

// /end .so hack
